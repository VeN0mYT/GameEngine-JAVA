#vertex
#version 330 core

layout(location = 0) in vec3 aPos;
layout(location = 1) in vec2 aTex;
layout(location = 2) in vec3 aColor;
layout(location = 3) in vec3 aNormal;

out vec3 fragPos;
out vec3 normal;
out vec2 texCoord;
out vec3 vertColor;

uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;

// Add these new uniforms ðŸ‘‡
uniform float time = 0.1;       // passed from CPU each frame
uniform float amplitude = 0.3;  // how strong the wave motion is
uniform float frequency = 5.0;  // how many waves per unit distance

void main()
{
    // ====== WAVE MOTION ======
    // Create a wavy displacement based on position + time
    float wave = sin(aPos.x * frequency + time) * cos(aPos.y * frequency + time);
    vec3 displacedPos = aPos + aNormal * wave * amplitude;

    // ====== Standard transformations ======
    fragPos = vec3(model * vec4(displacedPos, 1.0));
    normal = mat3(transpose(inverse(model))) * aNormal;
    texCoord = aTex;
    vertColor = aColor;

    gl_Position = projection * view * vec4(fragPos, 1.0);
}


#fragment
#version 330 core

in vec3 fragPos;
in vec3 normal;
in vec2 texCoord;
in vec3 vertColor;

out vec4 FragColor;

uniform sampler2D texture_diffuse1;
uniform bool hasTexture = false;
uniform bool hasLight = false;

uniform vec3 lightPosition;
uniform vec3 lightColor;
uniform float lightIntensity = 1.0;
uniform vec3 viewPos;

uniform vec3 color = vec3(1,1,1);
uniform bool hasColor = true;

uniform bool isDirectional = true;

uniform vec3 ambientColor = vec3(0.1, 0.1, 0.1);



void main()
{

    vec3 baseColor = vertColor;

    if(!hasColor)
        baseColor = color;

    // Use texture if provided
    if (hasTexture)
        baseColor = texture(texture_diffuse1, texCoord).rgb;

    vec3 finalColor = baseColor;

    // Apply lighting only if available
    if (hasLight) {
        vec3 ambient = ambientColor;


        vec3 norm = normalize(normal);

        vec3 lightDir;
        if (isDirectional)
            lightDir = normalize(lightPosition); // treat lightPosition as direction
        else
            lightDir = normalize(lightPosition - fragPos);

        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * lightColor;

        vec3 viewDir = normalize(viewPos - fragPos);
        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32);
        vec3 specular = spec * lightColor * lightIntensity;

        finalColor = (ambient + diffuse + specular) * baseColor;
    }

    FragColor = vec4(finalColor, 1.0);
}

